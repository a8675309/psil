// Core
program = { SOI ~ expr_list? ~ EOI }
expr_list = { expr+ }
expr = { atom | special_form | invocation }
atom = { number | boolean | string | void | lambda | name }
special_form = { if_form | define | do_form }
invocation = { "(" ~ name ~ expr_list? ~ ")" }

// Special forms
// These are separate from invocation because they do not use applicative-order evaluation.
if_form = { "(" ~ "if" ~ expr ~ expr ~ expr ~ ")" } // cannot use reserved keyword as pest name
define = { "(" ~ "define" ~ name ~ expr ~ ")" }
do_form = { "(" ~ "do" ~ expr_list? ~ ")" }

// Atoms
number = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
boolean = @{ "true" | "false" }
string = ${ "\"" ~ string_inner ~ "\"" }
void = @{ "void" }
lambda = { "{"  ~ params? ~ expr ~ "}" }
params = { "|" ~ name+ ~ "|"}
name = @{ char+ } // this is intentionally quite inclusive

char = { "!" | '#'..'&' | '*'..'Z' | '^'..'_' | 'a'..'z' }
string_inner = @{ string_char* }
string_char = { !("\"" | "\\") ~ ANY }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" | "\r\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}
